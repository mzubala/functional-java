plugins {
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'io.freefair.lombok' version '8.4'
}

group 'pl.com.bottega'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    integration {
        compileClasspath += sourceSets.test.output + sourceSets.test.compileClasspath
        runtimeClasspath += sourceSets.test.output + sourceSets.test.runtimeClasspath
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'io.vavr:vavr:0.10.4'
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:postgresql:1.19.3'
    testImplementation 'org.assertj:assertj-core:3.21.0'
    testImplementation 'io.projectreactor:reactor-test:3.4.10'
    testImplementation 'net.datafaker:datafaker:1.5.0'
    testImplementation 'org.assertj:assertj-vavr:0.4.2'
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    useJUnitPlatform()
    shouldRunAfter test
}

check.dependsOn(integrationTest)

test {
    useJUnitPlatform()
}
